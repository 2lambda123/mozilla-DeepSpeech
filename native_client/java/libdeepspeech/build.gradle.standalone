apply plugin: 'maven'
apply plugin: 'java'

allprojects {
    sourceSets {
        main.java.srcDirs = [ 'src/main/java/org/deepspeech/libdeepspeech/' ]
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation 'junit:junit:4.12'
}

task printABIs {
    print ABI_FILTERS.split(';').join(" ")
}

task sourcesJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            pom.packaging  = 'aar'
            pom.name       = 'libdeepspeech'
            pom.groupId    = 'org.deepspeech'
            pom.artifactId = 'libdeepspeech'
            pom.version    = dsVersionString + (project.hasProperty('snapshot') ? '-SNAPSHOT' : '')

            pom.project {
                description 'Speech recognition library'
                url 'https://github.com/mozilla/DeepSpeech'

                licenses {
                    license {
                        name 'MPL-2.0'
                        url 'https://www.mozilla.org/en-US/MPL/2.0/'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'deepspeech'
                        name 'DeepSpeech authors'
                        email 'deepspeechs@lists.mozilla.org'
                    }
                }

                scm {
                    connection 'https://github.com/mozilla/DeepSpeech.git'
                    developerConnection 'https://github.com/mozilla/DeepSpeech.git'
                    url 'https://github.com/mozilla/DeepSpeech'
                }
            }

            repository(url: "file://${project.buildDir}/maven")
        }
    }
}

task zipMavenArtifacts(type: Zip) {
    from "${project.buildDir}/maven"

    include '**/*.aar'
    include '**/*.jar'
    include '**/*.pom'
    include '**/*.md5'
    include '**/*.sha1'
    include '**/*maven-metadata.xml*'

    archiveName "${archivesBaseName}.maven.zip"
    includeEmptyDirs = false
    destinationDir(file("${project.buildDir}"))
}
